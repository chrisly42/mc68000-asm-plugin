plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.4.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

group = 'de.platon42'
version = '0.9'
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

/*
  To run tests in IntelliJ use these VM Options for run configuration 
  -ea -Didea.system.path=build/idea-sandbox/system-test -Didea.config.path=build/idea-sandbox/config-test -Didea.plugins.path=build/idea-sandbox/plugins-test
 */

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
//    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xjvm-default=all"
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    setVersion("2021.3.3") // LATEST-EAP-SNAPSHOT
    setUpdateSinceUntilBuild(false)
//    setPlugins(["com.intellij.java"])
}

runPluginVerifier {
    ideVersions = ["IC-203.6682.168", "IC-213.7172.25", // 2020.3 - 2021.3.3
                   "CL-203.8084.11", // 2020.3
                   "CL-211.7628.27", // 2021.1
                   "CL-212.5712.21", // 2021.2
                   "CL-213.7172.20"] // 2021.3.4
    downloadDir = System.getProperty("user.home") + "/.gradle/caches/modules-2/files-2.1/com.jetbrains.intellij.idea/verifier"
}

patchPluginXml {
    setChangeNotes("""
  <h4>V0.9 (07-Apr-22)</h4>
    <ul>
    <li>Maintenance. Updated all dependencies to the latest versions.
    <li>Bugfix: Fixed condition code for asr/lsr/lsl, which is has a different behaviour for V flag than asl.
    </ul>
  <p>Full changelog available at <a href="https://github.com/chrisly42/mc68000-asm-plugin#changelog">Github project site</a>.</p>
""")
}

sourceSets.main.java.srcDirs('src/main/gen', 'src/main/java')

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(false)
    }
}

publishPlugin {
    setToken(intellijPublishToken)
}